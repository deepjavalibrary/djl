group "ai.djl.dlr"

dependencies {
    api project(":api")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
    testRuntimeOnly project(":pytorch:pytorch-engine")
    testRuntimeOnly "ai.djl.dlr:dlr-native-auto:${dlr_version}"
    testRuntimeOnly "ai.djl.pytorch:pytorch-native-auto:${pytorch_version}-SNAPSHOT"
}

processResources {
    outputs.dir file("${project.buildDir}/jnilib")
    doLast {
        def url = "https://publish.djl.ai/dlr-${dlr_version}/jnilib/${djl_version}"
        def files = [
                "osx-x86_64/libdjl_dlr.dylib" : "osx-x86_64/libdjl_dlr.dylib",
                "linux-x86_64/libdjl_dlr.so"  : "linux-x86_64/libdjl_dlr.so"
        ]
        def classesDir = "${project.buildDir}/jnilib"
        files.each { entry ->
            project.logger.lifecycle("Downloading ${url}/${entry.key}")
            def file = new File("${classesDir}/${entry.value}")
            if (file.exists()) {
                project.logger.lifecycle("prebuilt or cached file found for ${entry.value}")
            } else {
                file.getParentFile().mkdirs()
                new URL("${url}/${entry.key}").withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
        // write properties
        def propFile = file("${classesDir}/dlr.properties")
        propFile.text = "version=${version}\n"

        copy {
            from classesDir
            into "${project.buildDir}/classes/java/main/jnilib"
        }
    }
}

test {
    environment "PATH", "src/test/bin:${environment.PATH}"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "dlr-engine"
            pom {
                name = "DJL Engine Adapter for DLR"
                description = "Deep Java Library (DJL) Engine Adapter for DLR"
                url = "http://www.djl.ai/dlr/${project.name}"
            }
        }
    }
}
