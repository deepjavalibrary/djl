plugins {
    id 'application'
}

dependencies {
    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"
    implementation project(":basicdataset")
    implementation project(":model-zoo")
    implementation project(":extensions:timeseries")
    implementation project(":extensions:tokenizers")
    implementation project(":extensions:audio")

    runtimeOnly project(":engines:pytorch:pytorch-model-zoo")
    runtimeOnly project(":engines:tensorflow:tensorflow-model-zoo")
    runtimeOnly project(":engines:mxnet:mxnet-model-zoo")
    runtimeOnly project(":engines:onnxruntime:onnxruntime-engine")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
}

application {
    mainClass = System.getProperty("main", "ai.djl.examples.inference.cv.ObjectDetection")
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
}

tasks.register('listmodels', JavaExec) {
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "ai.djl.examples.inference.ListModels"
}
tasks.distTar.enabled = false
tasks.distZip.enabled = false
