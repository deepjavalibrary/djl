import java.net.HttpURLConnection
import java.net.URL
import java.util.Base64

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 34
    namespace "ai.djl.android.pytorch_native"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["jnilib/"]
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

// TODO: Making publish as an individual gradle file
signing {
    required(project.hasProperty("staging") || project.hasProperty("snapshot"))
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                pom {
                    name = "PyTorch Android native library"
                    description = "PyTorch Android native library binary distribution"
                    url = "http://www.djl.ai/android"
                    packaging = "aar"

                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0"
                        }
                    }

                    scm {
                        connection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                        developerConnection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                        url = "https://github.com/deepjavalibrary/djl"
                        tag = "HEAD"
                    }

                    developers {
                        developer {
                            name = "DJL.AI Team"
                            email = "djl-dev@amazon.com"
                            organization = "Amazon AI"
                            organizationUrl = "https://amazon.com"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                if (project.hasProperty("snapshot")) {
                    name = "snapshot"
                    url = uri("https://central.sonatype.com/repository/maven-snapshots/")
                    credentials {
                        username = findProperty("sonatypeUsername").toString()
                        password = findProperty("sonatypePassword").toString()
                    }
                } else if (project.hasProperty("staging")) {
                    name = "staging"
                    url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
                    credentials {
                        username = findProperty("sonatypeUsername").toString()
                        password = findProperty("sonatypePassword").toString()
                    }
                } else {
                    name = "local"
                    url = "build/repo"
                }
            }
        }
    }
}

// Post-publish task to make deployment visible in Central Publisher Portal.
// See https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#ensuring-deployment-visibility-in-the-central-publisher-portal
if (project.hasProperty("staging")) {
    def url = "https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/${project.group}"
    def username = findProperty("sonatypeUsername").toString()
    def password = findProperty("sonatypePassword").toString()
    def token = Base64.encoder.encodeToString("${username}:${password}".bytes)

    tasks.register("postPublish") {
        doLast {
            HttpURLConnection conn = (HttpURLConnection) new URL(url).openConnection()
            conn.requestMethod = "POST"
            conn.setRequestProperty("Authorization", "Bearer ${token}")
            int status = conn.responseCode
            if (status != HttpURLConnection.HTTP_OK) {
                throw new GradleException("Failed to POST '${url}'. Received status code ${status}: ${conn.responseMessage}")
            }
        }
    }

    tasks.named("publish") {
        finalizedBy("postPublish")
    }
}

tasks.register('processResources') {
    doLast {
        def url = "https://publish.djl.ai/pytorch/${pytorch_version}/jnilib/${djl_version}/android"
        def abis = ["armeabi-v7a", "arm64-v8a", "x86", "x86_64"]
        abis.each { abi ->
            def downloadPath = new URL("${url}/${abi}/libdjl_torch.so")
            def file = file("${project.projectDir}/jnilib/${abi}/libdjl_torch.so")
            if (!file.exists()) {
                project.logger.lifecycle("${url}/${abi}/libdjl_torch.so")
                file.getParentFile().mkdirs()
                downloadPath.withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
    }
}

assemble.dependsOn processResources
