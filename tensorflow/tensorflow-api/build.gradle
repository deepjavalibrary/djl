group "ai.djl.tensorflow"

configurations {
    exclusion
}

dependencies {
    api "org.bytedeco:javacpp:1.5.4"
    api "com.google.protobuf:protobuf-java:3.8.0"
    api("org.tensorflow:tensorflow-core-api:0.3.0") {
        exclude group: "org.bytedeco", module: "javacpp"
    }

    exclusion "org.bytedeco:javacpp:1.5.4"
    exclusion "com.google.protobuf:protobuf-java:3.8.0"
}

jar {
    from {
        (configurations.compileClasspath - configurations.exclusion).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL release for TensorFlow core api"
                description = "Deep Java Library (DJL) release for TensorFlow core api"
                url = "http://www.djl.ai/tensorflow/${project.name}"

                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies."*".findAll() {
                        it.artifactId.text().startsWith("tensorflow-") || it.artifactId.text().startsWith("ndarray")
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}
