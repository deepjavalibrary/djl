group "ai.djl.tensorflow"

configurations {
    exclusion
}

dependencies {
    // javacpp bug fix https://github.com/bytedeco/javacpp/commit/7f27899578dfa18e22738a3dd49701e1806b464a
    api "org.bytedeco:javacpp:${javacpp_version}"
    api "com.google.protobuf:protobuf-java:${protobuf_version}"
    api("org.tensorflow:tensorflow-core-api:0.3.1") {
        exclude group: "org.bytedeco", module: "javacpp"
    }

    exclusion "org.bytedeco:javacpp:${javacpp_version}"
    exclusion "com.google.protobuf:protobuf-java:${protobuf_version}"
}

jar {
    from {
        (configurations.compileClasspath - configurations.exclusion).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL release for TensorFlow core api"
                description = "Deep Java Library (DJL) release for TensorFlow core api"
                url = "http://www.djl.ai/tensorflow/${project.name}"

                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies."*".findAll() {
                        it.artifactId.text().startsWith("tensorflow-") || it.artifactId.text().startsWith("ndarray")
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}
