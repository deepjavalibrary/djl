buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.googlejavaformat:google-java-format:1.22.0'
    }
}

apply plugin: JavaFormatterPlugin

check.dependsOn verifyJava

import com.google.googlejavaformat.java.Formatter
import com.google.googlejavaformat.java.ImportOrderer
import com.google.googlejavaformat.java.JavaFormatterOptions
import com.google.googlejavaformat.java.Main
import com.google.googlejavaformat.java.RemoveUnusedImports

class JavaFormatterPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.task('formatJava') {
            doLast {
                Main formatter = new Main(new PrintWriter(System.out, true), new PrintWriter(System.err, true), System.in)
                for (item in project.sourceSets) {
                    for (File file : item.getAllSource()) {
                        if (!file.getName().endsWith(".java") || file.getAbsolutePath().contains("generated-src")) {
                            continue
                        }
                        if (formatter.format("-a", "-i", file.getAbsolutePath()) != 0) {
                            throw new GradleException("Format java failed: " + file.getAbsolutePath())
                        }
                    }
                }
            }
        }

        project.task('verifyJava') {
            //declare normalized inputs and outputs to allow Gradle to cache the task or skip it if inputs do not change
            def resultFilePath = "build/verifyJava-result.txt"
            inputs.files(project.sourceSets*.allSource)
                    .withPathSensitivity(PathSensitivity.RELATIVE)
                    .withPropertyName('sourceFiles')
            inputs.files(project.fileTree('generated-src'))
                    .withPropertyName('generatedSourceFiles')
                    .withPathSensitivity(PathSensitivity.RELATIVE)
            outputs.file(project.file(resultFilePath))
            //there is no reason not to cache the task if Gradle needs to, so enable it
            outputs.cacheIf { true }

            doLast {
                Main formatter = new Main(new PrintWriter(System.out, true), new PrintWriter(System.err, true), System.in)
                for (item in project.sourceSets) {
                    for (File file : item.getAllSource()) {
                        if (!file.getName().endsWith(".java") || file.getAbsolutePath().contains("generated-src")) {
                            continue
                        }
                        if (formatter.format("-a", "-n", "--set-exit-if-changed", file.getAbsolutePath()) != 0) {
                            throw new GradleException("File not formatted: " + file.getAbsolutePath()
                                    + System.lineSeparator()
                                    + "In order to reformat your code, run './gradlew formatJava' (or './gradlew fJ' for short)"
                                    + System.lineSeparator()
                                    + "See https://github.com/deepjavalibrary/djl/blob/master/docs/development/development_guideline.md#coding-conventions for more details")
                        }
                    }
                }
                project.file(resultFilePath).write('Success')
            }
        }
    }
}
