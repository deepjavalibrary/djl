if (JavaVersion.current() < JavaVersion.VERSION_19) {
    apply plugin: "com.github.spotbugs"
    spotbugs {
        excludeFilter = file("${rootProject.projectDir}/tools/conf/findbugs-exclude.xml")
        ignoreFailures = false
        spotbugsTest.enabled = true
    }
    spotbugsMain {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
    spotbugsTest {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

apply plugin: "pmd"
pmd {
    ignoreFailures = false
    pmdTest.enabled = false
    ruleSets = [] // workaround pmd gradle plugin bug
    ruleSetFiles = files("${rootProject.projectDir}/tools/conf/pmd.xml")
}
tasks.withType(Pmd) {
    reports {
        xml.required = true
        html.required = true
    }
}

apply plugin: "checkstyle"
checkstyle {
    toolVersion = "10.14.2"
    ignoreFailures = false
    checkstyleTest.enabled = true
    //use relative paths to ensure build caching works across different locations
    configDirectory = file(relativePath(file("${rootProject.projectDir}/tools/conf")))
}
checkstyleMain {
    classpath += configurations.compileClasspath
}
tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.8"
}
jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

test.finalizedBy jacocoTestReport
build.dependsOn javadoc
