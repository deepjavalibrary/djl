group "ai.djl.fasttext"

dependencies {
    api project(":api")
    api project(":basicdataset")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    // ModelZoo requires at least one engine in the class path
    testImplementation project(":pytorch:pytorch-engine")
    testImplementation "ai.djl.pytorch:pytorch-native-auto:${pytorch_version}-SNAPSHOT"
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

processResources {
    outputs.dir file("${project.buildDir}/classes/java/main/native/lib")
    doLast {
        def url = "https://publish.djl.ai/fasttext-${fasttext_version}/jnilib/${djl_version}"
        def files = [
                "linux-x86_64": "libjni_fasttext.so",
                "osx-x86_64"  : "libjni_fasttext.dylib"
        ]
        def jniDir = "${project.buildDir}/jnilib"
        files.each { entry ->
            def downloadPath = new URL("${url}/${entry.key}/${entry.value}")
            def file = file("${jniDir}/${entry.key}/${entry.value}")
            if (!file.exists()) {
                project.logger.lifecycle("${downloadPath}")
                file.getParentFile().mkdirs()
                downloadPath.withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
        def classDir = "${project.buildDir}/classes/java/main/native/lib"
        file(classDir).mkdirs()
        copy {
            from jniDir
            into classDir
        }
        // write properties
        def versionName = "${fasttext_version}"
        boolean isRelease = project.hasProperty("release") || project.hasProperty("staging")
        if (!isRelease) {
            versionName += String.format("-%s", new Date().format('yyyyMMdd'))
        }
        def propFile = file("${classDir}/fasttext.properties")
        propFile.text = "version=${versionName}\n"

        // for ci to upload to S3
        def ciDir = "${project.projectDir}/jnilib/${djl_version}"
        copy {
            from jniDir
            into ciDir
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                artifactId "fasttext-engine"
                name = "Fasttext Engine Adapter"
                description = "Fasttext Engine Adapter for DJL"
                url = "http://www.djl.ai/fasttext/${project.name}"
            }
        }
    }
}

apply from: file("${rootProject.projectDir}/tools/gradle/cpp-formatter.gradle")

task compileJNI {
    doFirst {
        if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains("mac")
                || System.properties['os.name'].toLowerCase(Locale.ROOT).contains("linux")) {
            exec {
                commandLine 'bash', 'build.sh'
            }
        } else {
            throw new IllegalStateException("Unknown Architecture " + System.properties['os.name'])
        }
    }
}
