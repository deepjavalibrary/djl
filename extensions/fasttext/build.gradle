group "ai.djl.fasttext"

dependencies {
    api project(":api")

    testImplementation project(":basicdataset")
    testImplementation project(":testing")
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

compileJava.dependsOn(processResources)

processResources {
    def baseResourcePath = "${project.projectDir}/build/resources/main"
    outputs.dir file("${baseResourcePath}/native/lib")
    doLast {
        def url = "https://publish.djl.ai/fasttext-${fasttext_version}/jnilib/${djl_version}"
        def files = [
                "linux-x86_64": "libjni_fasttext.so",
                "osx-x86_64"  : "libjni_fasttext.dylib",
                "osx-aarch64" : "libjni_fasttext.dylib"
        ]
        def jnilibDir = "${project.projectDir}/jnilib/${djl_version}"
        files.each { entry ->
            def file = new File("${jnilibDir}/${entry.key}/${entry.value}")
            if (file.exists()) {
                project.logger.lifecycle("prebuilt or cached file found for ${entry.value}")
            } else if (!project.hasProperty("jni")) {
                project.logger.lifecycle("Downloading ${url}/${entry.key}")
                file.getParentFile().mkdirs()
                def downloadPath = new URL("${url}/${entry.key}/${entry.value}")
                downloadPath.withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
        copy {
            from jnilibDir
            into "${baseResourcePath}/native/lib"
        }

        // write properties
        def propFile = file("${baseResourcePath}/native/lib/fasttext.properties")
        propFile.text = "version=${fasttext_version}-${version}\n"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                artifactId "fasttext-engine"
                name = "Fasttext Engine Adapter"
                description = "Fasttext Engine Adapter for DJL"
                url = "http://www.djl.ai/extensions/${project.name}"
            }
        }
    }
}

apply from: file("${rootProject.projectDir}/tools/gradle/cpp-formatter.gradle")

tasks.register('compileJNI') {
    doFirst {
        if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains("mac")
                || System.properties['os.name'].toLowerCase(Locale.ROOT).contains("linux")) {
            exec {
                commandLine 'bash', 'build.sh'
            }
        } else {
            throw new IllegalStateException("Unknown Architecture " + System.properties['os.name'])
        }

        // for ci to upload to S3
        def ciDir = "${project.projectDir}/jnilib/${djl_version}"
        copy {
            from "${project.buildDir}/jnilib"
            into ciDir
        }
        delete System.getProperty("user.home") + "/.djl.ai/fasttext"
    }
}

clean.doFirst {
    delete System.getProperty("user.home") + "/.djl.ai/fasttext"
}
