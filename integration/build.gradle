plugins {
    id 'application'
    id 'jacoco'
}

dependencies {
    api "commons-cli:commons-cli:${commons_cli_version}"
    api "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"
    api project(":basicdataset")
    api project(":model-zoo")
    api project(":testing")
    api("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    runtimeOnly project(":mxnet:mxnet-model-zoo")
    runtimeOnly "ai.djl.mxnet:mxnet-native-auto:${mxnet_version}"
    runtimeOnly project(":pytorch:pytorch-model-zoo")
    runtimeOnly "ai.djl.pytorch:pytorch-native-auto:${pytorch_version}-SNAPSHOT"
    runtimeOnly project(":tensorflow:tensorflow-model-zoo")
    runtimeOnly "ai.djl.tensorflow:tensorflow-native-auto:${tensorflow_version}"
    runtimeOnly project(":ml:xgboost")

    if (System.getProperty("ai.djl.default_engine") == "OnnxRuntime") {
        // onnxruntime requires user install libgomp.so.1 manually, exclude from default dependency
        runtimeOnly project(":onnxruntime:onnxruntime-engine")
    } else if (System.getProperty("ai.djl.default_engine") == "TFLite") {
        runtimeOnly project(":tflite:tflite-engine")
        runtimeOnly "ai.djl.tflite:tflite-native-auto:${tflite_version}"
    } else if (System.getProperty("ai.djl.default_engine") == "DLR") {
        runtimeOnly project(":dlr:dlr-engine")
        runtimeOnly "ai.djl.dlr:dlr-native-auto:${dlr_version}"
    } else if (System.getProperty("ai.djl.default_engine") == "PaddlePaddle") {
        runtimeOnly project(":paddlepaddle:paddlepaddle-model-zoo")
        runtimeOnly "ai.djl.paddlepaddle:paddlepaddle-native-auto:${paddlepaddle_version}"
    }
}

task copyDependencies(type: Copy) {
    into "build/dependencies"
    from configurations.runtimeClasspath
}

application {
    getMainClass().set(System.getProperty("main", "ai.djl.integration.IntegrationTest"))
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    jvmArgs "-Xverify:none"
}

jacocoTestReport {
    group = "Reporting"

    javaProjects().each { p ->
        if ([":api", ":basicdataset"].contains(p.getPath())) {
            additionalSourceDirs files((Set<File>) p.sourceSets.main.allJava.srcDirs)
            additionalClassDirs((FileCollection) p.sourceSets.main.output)
        }
    }

    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/ai/djl/integration/**"])
        })
    }

    reports {
        xml.enabled true
        csv.enabled false
    }

    // Work-around to allow us to build list of executionData files in doFirst
    onlyIf = {
        true
    }
}

task debugEnv(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperties.put("ai.djl.logging.level", "debug")
    main = "ai.djl.integration.util.DebugEnvironment"
}

tasks.distTar.enabled = false
test.finalizedBy project.tasks.jacocoTestReport, project.tasks.jacocoTestCoverageVerification

jacocoTestCoverageVerification {
    javaProjects().each { p ->
        if ([":api", ":basicdataset"].contains(p.getPath())) {
            additionalSourceDirs files((Set<File>) p.sourceSets.main.allJava.srcDirs)
            additionalClassDirs((FileCollection) p.sourceSets.main.output)
        }
    }

    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/ai/djl/integration/**"])
        })
    }

    violationRules {
        rule {
            limit {
                if (System.getProperty("ai.djl.default_engine") == "PyTorch") {
                    minimum = 0.4
                } else if (System.getProperty("ai.djl.default_engine") == "TensorFlow") {
                    minimum = 0.29
                } else {
                    // TODO: improve coverage by running integration tests with 3 engines
                    minimum = 0.4
                }
            }
        }
    }
}
